extension java::Naming;
extension java::GeneratorCommons;

boolean hasOpenAPITypes(JMM::OpenAPI3Specification spec):
	getOpenAPITypes(spec).size > 0;
	
List[JMM::OpenAPIType] getOpenAPITypes(JMM::OpenAPI3Specification spec):
	spec.clientDependency.supplier.typeSelect(uml::Package).ownedElement.typeSelect(JMM::OpenAPIType);
	
List[JMM::OpenAPI3Specification] getDependentSpecs(JMM::OpenAPI3Specification spec):
	spec.clientDependency.supplier.typeSelect(JMM::OpenAPI3Specification);
	
Void registerSpecDependency( uml::Component pSource, uml::Component pTarget, String pReference ):
    JAVA com.anaptecs.jeaf.fwk.generator.util.OpenAPIHelper.registerSpecDependency( org.eclipse.uml2.uml.Component, org.eclipse.uml2.uml.Component, java.lang.String );
	
boolean hasOpenAPIEnums(JMM::OpenAPI3Specification spec):
	getOpenAPIEnums(spec).size > 0;
	
List[JMM::OpenAPIEnumeration] getOpenAPIEnums(JMM::OpenAPI3Specification spec):
	spec.clientDependency.supplier.typeSelect(uml::Package).ownedElement.typeSelect(JMM::OpenAPIEnumeration);
	
boolean referencesRESTResources(JMM::OpenAPI3Specification spec):
	getRESTResources(spec).size > 0;
	
List[JMM::RESTResource] getRESTResources(JMM::OpenAPI3Specification spec):
	spec.clientDependency.supplier.typeSelect(JMM::RESTResource);
	
List[JMM::RESTOperation] getRESTOperations(JMM::RESTResource service, String pPath):
   service.ownedOperation.typeSelect(JMM::RESTOperation).select(e|e.path == pPath);
   
Set[String] getPaths(JMM::RESTResource service):
   service.ownedOperation.typeSelect(JMM::RESTOperation).path.toSet();
	
String getYAMLStyleComment(uml::Element element, String indentation):
    element.ownedComment.get(0).body.replaceAll("\n", "\n" + indentation + "# ");

String getCommentForOpenAPIDescription(uml::Element element, String indentation):
    element.ownedComment.get(0).body.replaceAll("\n", "\n" + indentation);
    
String getOpenAPIType(uml::Type type, JMM::OpenAPI3Specification spec):
    JAVA com.anaptecs.jeaf.fwk.generator.util.OpenAPIHelper.getOpenAPIType(org.eclipse.uml2.uml.Type, org.eclipse.uml2.uml.Component);
    
String getOpenAPIFormat(uml::Type type):
    JAVA com.anaptecs.jeaf.fwk.generator.util.OpenAPIHelper.getOpenAPIFormat(org.eclipse.uml2.uml.Type);
    
boolean hasOpenAPIFormat(uml::Type type):
    getOpenAPIFormat(type) != null;
    
boolean isBasicOpenAPIType(uml::Type type):
    JAVA com.anaptecs.jeaf.fwk.generator.util.OpenAPIHelper.isBasicOpenAPIType(org.eclipse.uml2.uml.Type);
    
Void registerLocalType(uml::NamedElement localClass, JMM::OpenAPI3Specification spec):
    JAVA com.anaptecs.jeaf.fwk.generator.util.OpenAPIHelper.registerLocalType(org.eclipse.uml2.uml.NamedElement, org.eclipse.uml2.uml.Component);
    
List[uml::Class] getAllSubclasses(uml::Class class) :
   JAVA com.anaptecs.jeaf.fwk.generator.util.ClassUtil.getAllSubclasses(org.eclipse.uml2.uml.Class);
   
boolean isTopOfClassHierarchy(uml::Class class) :
   class.superClass.isEmpty == true && getAllSubclasses(class).size > 0;
	
String toOpenAPIContentType(JMM::MediaType mediaType) :
   toOpenAPIContentType(mediaType.toString());
   
String toOpenAPIContentType(String mediaType) :
   JAVA com.anaptecs.jeaf.fwk.generator.util.OpenAPIHelper.toOpenAPIContentType(java.lang.String);
   
boolean isOpenAPIParameterType(uml::Element element):
   getOpenAPIParameterType(element) != null;

String getOpenAPIParameterType(uml::Element param):
   JAVA com.anaptecs.jeaf.fwk.generator.util.OpenAPIHelper.getOpenAPIParameterType(org.eclipse.uml2.uml.Element);
   
boolean isOpenAPIInputParameter(uml::Parameter param):
   param.isInputParameter() && param.isOpenAPIParameterType();

List[uml::Parameter] getOpenAPIInputParameters(uml::Operation operation):   
   operation.ownedParameter.select(p| p.isOpenAPIInputParameter());   

boolean hasOpenAPIInputParameters(uml::Operation operation):
   getOpenAPIInputParameters(operation).size > 0;
   
boolean isBeanParamater(uml::Parameter param):
   param.isStereotypeApplied("BeanParam") || param.type.isStereotypeApplied("BeanParam");
   
List[uml::Parameter] getBeanParams(uml::Operation operation):
   operation.ownedParameter.select(e|e.isBeanParamater());
   
boolean hasBeanParameters(uml::Operation operation):
   getBeanParams(operation).size > 0;
   
boolean hasOpenAPIProperties(uml::Class pClass):
   getOpenAPIProperties(pClass).size > 0;
   
List[uml::Property] getOpenAPIProperties(uml::Element class):
   class.getAllAttributesFromHierarchy().select(e|e.isOpenAPIParameterType());
   
List[uml::Property] getAllAttributesFromHierarchy(uml::Element class):
  JAVA com.anaptecs.jeaf.fwk.generator.util.OpenAPIHelper.getAllAttributesFromHierarchy(org.eclipse.uml2.uml.Element);
   
   
boolean isRequired(uml::TypedElement type):
   type.isStereotypeApplied("NotNull") || type.isStereotypeApplied("NotEmpty") || type.isStereotypeApplied("NotBlank") || type.type.isPrimitiveType();
   
List[uml::Property] getRequiredProperties(uml::Class class):
    class.ownedAttribute.select(e|e.isRequired());
    
boolean hasRequiredProperties(uml::Class class):
    getRequiredProperties(class).size > 0;
    
boolean supportsRequestBody(JMM::HTTPMethod httpMethod):
  httpMethod.toString().toLowerCase() == "post" || httpMethod.toString().toLowerCase() == "put" || httpMethod.toString().toLowerCase() == "patch";

boolean hasRequestBody(uml::Operation operation, JMM::HTTPMethod httpMethod):
  getRequestBodyParameters(operation).size == 1  && httpMethod.supportsRequestBody() == true;
  
List[uml::Parameter] getRequestBodyParameters(uml::Operation operation):
  operation.ownedParameter.select(e|e.isInputParameter() && e.isOpenAPIInputParameter() == false && e.isBeanParamater() == false);
  
Set[JMM::MediaType] getAllMediaTypes(Collection[JMM::MediaType] serviceMediaTypes, Collection[JMM::MediaType] operationMediaType):
  serviceMediaTypes.addAll(operationMediaType).toSet();
  
boolean hasReturnTypeVoid(uml::Operation operation):
  getReturnTypeName(operation) == "void";
  
uml::Parameter getReturnParameter(uml::Operation operation):
  operation.getReturnResult();
  
boolean hasExample(uml::NamedElement element):
  element.isStereotypeApplied("OpenAPIExample");
  
boolean isNullable(uml::Property property):
  if property.isStereotypeApplied("OpenAPIProperty") 
  then ((JMM::OpenAPIProperty) property).nullable 
  else
    if property.isStereotypeApplied("NotNull")
    then false
    else true;
  
  
boolean isReadOnly(uml::Property property):
  if property.isStereotypeApplied("OpenAPIProperty") 
  then ((JMM::OpenAPIProperty) property).accessType.name == "READ_ONLY" 
  else 
    property.isReadOnly;
  
boolean isWriteOnly(uml::Property property):
  if property.isStereotypeApplied("OpenAPIProperty") then ((JMM::OpenAPIProperty) property).accessType.name == "WRITE_ONLY" else false;
  
String getOperationID(uml::Operation operation, uml::Interface service):
   operation.name;
   
String getJAXRSStatusCodeName(String pJMMStatusCode):
  JAVA com.anaptecs.jeaf.fwk.generator.util.OpenAPIHelper.getJAXRSStatusCodeName(java.lang.String);  
  

  

